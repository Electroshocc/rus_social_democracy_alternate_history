title: Отто Браун
is-pinned-card: true
tags: advisor, reformist
card-image: img/portraits/BraunOtto.jpg
view-if: braun_advisor = 1
new-page: true 

= Отто Браун

Браун [? if spd_prussia = 1 : - ?][? if spd_prussia = 0 : - бывшый ?] министр-президент Пруссии, самой большой части Германии. Он - глава реформистского крыла, больше всего сотрудничающего с буржуазией.

- @coalition
- @prussian_bulwark
- @snap_election
- @root: Return to main

@coalition
title: Встретиться с коалицией
subtitle: Браун может улучшить отношения с правыми коалиционными партнёрами как в Пруссии, так и в Рейхстаге.
choose-if: advisor_action_timer <= 0 and (spd_prussia or in_weimar_coalition or in_grand_coalition or in_popular_front)
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] месяца(ев) до следующего действия. ?] [? if not spd_prussia : СДПГ не находится в прусском правительстве. ?]
on-arrival: advisor_action_timer = 6; coalition_dissent -= 1 if spd_in_government = 1 and coalition_dissent > 0; z_relation += 6; ddp_relation += 6; dvp_relation += 2; dvp_relation += 3 if in_grand_coalition

Мы улучшили отношения с [? if in_grand_coalition : Партией Центра, [+ ddp_name +], и ННП, ?][? if not in_grand_coalition: Партией Центра и [+ ddp_name +], ?] и уладили внутренний раздор.

@prussian_bulwark
title: Крепость Пруссия
subtitle: Как у министра-президента Пруссии, у Брауна есть возможность повлиять на прусскую полицию и правительство.
choose-if: advisor_action_timer <= 0 and spd_prussia = 1
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] месяца(ев) до следующего действия. ?][? if spd_prussia == 0 : СДПГ не контролирует Пруссию. ?]
on-arrival: advisor_action_timer = 6; prussian_affairs_timer -= 6; last_advisor_action = 1
go-to: prussian_affairs

@snap_election
title: Досрочные выборы
subtitle: Начать выборы до назначенного срока.
unavailable-subtitle: [? if chancellor != "Braun" and president != "Braun" : Браун должен быть канцлером или президентом. ?][? if advisor_action_timer > 0 : [+ advisor_action_timer +] месяца(ев) до следующего действия. ?] [? if president = "Braun" and presidential_powers : Клоун, ты ж сам запретил президенту это делать по конституции. ?]
choose-if: advisor_action_timer == 0 and (chancellor == "Braun" or (president == "Braun" and not presidential_powers))
on-arrival: advisor_action_timer = 6; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time+3; next_election_year = year if next_election_time >= time+3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; 

Мы назначили новые выборы на [+ next_election_month : month +] [+ next_election_year +].
