title: Макс Зейдевитц
is-pinned-card: true
tags: advisor, left
card-image: img/portraits/SeydewitzMax.jpg
view-if: seydewitz_advisor = 1
new-page: true 

= Макс Зейдевитц

Зейдевитц - бывший лидер молодёжной организации, ныне член левого крыла, поддерживающий классовую борьбу.

- @class_struggle
- @socialist_youth
- @against_toleration
- @root: Return to main

@class_struggle
title: Агитация за классовую борьбу
subtitle: Ядром социализма должна быть борьба рабочего класса с буржуазией.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] месяца(ев) до след. действия. ?]
choose-if: advisor_action_timer = 0
on-arrival: advisor_action_timer = 6; workers_spd += 4*(1-dissent); unemployed_spd += 3*(1-dissent); workers_spd += 4*(1-dissent) if nationalization_progress > 0; unemployed_spd += 3*(1-dissent) if nationalization_progress > 0; left_strength += 5; left_dissent -= 5; old_middle_spd -= 3; new_middle_spd -= 2

Наши высказывания о классовой борьбе нравятся представителям левых из рабочего класса, однако средний класс недоволен. [? if nationalization_progress > 0 : Наши успехи в наионализации помогли нашей кампании. ?]


@socialist_youth
title: Поддержать социалистическую молодёжь
subtitle: Нам нужно избавиться от репутации "Партии стариков", поддержав лояльные нам молодёжные организации. -1 ресурс.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] месяца(ев) до след. действия. ?] [? if resources <= 0 : Недостаточно ресурсов. ?]
choose-if: advisor_action_timer = 0 and resources > 0
on-arrival: advisor_action_timer = 6; resources -= 1; left_strength += 5; left_dissent -= 3; rb_strength += 50; rb_militancy += 0.025; workers_spd += 2*(1-dissent); new_middle_spd += 1;

Зейдевитц поддерживает наше молодёжное крыло, привлекая левых и увеличивая боеготовность Рейхсбаннера.

@against_toleration
title: Против толерации
subtitle: Разорвать наше соглашение с буржуазными партиями, начав тем самым новые выборы.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] месяца(ев) до след. действия. ?] [? if spd_toleration == 0 : На данный момент СДПГ не предоставляет никому толерацию. ?]
view-if: not constructive_vonc
choose-if: advisor_action_timer == 0 and spd_toleration
on-arrival: advisor_action_timer = 6; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time + 3; next_election_year = year if next_election_time >= time + 3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; spd_toleration = 0; left_strength += 8; pro_republic -= 4; new_middle_spd -= 3; rural_spd -= 3; old_middle_spd -= 3; catholics_spd -= 3; reformist_dissent += 8

Мы устли от толерации и вынесли вотум недоверия нынешнему правительству меньшинства.

Новые выборы были назначены на [+ next_election_month : month +] [+ next_election_year +].
