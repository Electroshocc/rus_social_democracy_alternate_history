title: Таможенный союз с Австрией
subtitle: МИД пытается наладить связи с Австрией...
new-page: true
on-arrival: customs_union_seen = 1
tags: event
view-if: year = 1931 and month >= 3 and foreign_minister_party != "SPD" and foreign_minister_party != "KPD" and customs_union == 0 and not eu
max-visits: 1

# TODO: this should happen regardless of the current state of the government...

= Таможенный союз с Австрией

[? if spd_in_government: The [+ foreign_minister_party +] министр иностранных дел предлагает нам заключить с Австрие таможенный союз. Мы можем либо согласиться с идеей, либо отказать. ?]
[? if not spd_in_government: Правительство, которым управляет канцлер [+ chancellor +] предлагает таможенный союз с Австрией. Скорее всего их решение не будет зависеть от нашего выбора. ?]


Националисты давно мечтали о более близких связях с Австрийцами, с конечной целью объединения двух германоязычных стран. Таможенный союз с ними станет первым шагом к этому. В дополнение к этому, многие верят, что союз улучшит наше нынешнее экономическое положение.

Однако страны Антанты, в особенности Франция, явно недовольны данным решением. Такое соглашение нарушит Версальский договор и может повлечь за собой санкции, нанося ещё больший ущерб нашей экономике.

- @let_it_happen: Позволить этому случиться.
- @end_coalition: Остановить их!
- @end_toleration: Попытаться остановить их!

@let_it_happen
on-arrival: banking_crisis_timer = 2; customs_union = 1; west_relation -= 2; east_relation -= 2

Был заключен таможенный союз. Тут же Франция и прочие наши соседи выразили протест. Долгосрочный ответ нам ещё предстоит увидеть.

@end_coalition
view-if: spd_in_government = 1
on-arrival: coalition_dissent += 1; dvp_relation -= 5; z_relation -= 4; z_relation -= 3 if chancellor_party == "Z"; dvp_relation -= 3 if chancellor_party == "Z"

Мы пытаемся образумить наших союзников по коалиции, предоекая недовольство соседей. Таможенный союз не был заключён, однако Центр и ННП недовольны.


@end_toleration
view-if: spd_toleration = 1
subtitle: Это сложно; все партии справа от нас поддерживают данное решение.
go-to: end_toleration_success if spd_r + kpd_r + ddp_r >= 50; end_toleration_fail if spd_r + kpd_r + ddp_r < 50

@end_toleration_success
on-arrival: time_to_election = 3; spd_in_government = 0; in_grand_coalition = 0; spd_toleration = 0;
call: set_next_election_time

При поддержке КПГ и [+ ddp_name +], мы успешно остановили формирование таможенного союза. Это приводит к расформированию правительства. Канцлер [+ chancellor +] уходит в отставку.

Новые выборы были назначены на [+ next_election_month : month +] [+ next_election_year +].

@end_toleration_fail
on-arrival: banking_crisis_timer = 2; customs_union = 1; west_relation -= 2; east_relation -= 2

К сожалению, правительство объединилось с правыми партиями, чтобы победить нас в голосовании. Таможенный союз будет сформирован.
