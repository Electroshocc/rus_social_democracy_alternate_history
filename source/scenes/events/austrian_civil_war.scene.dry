title: Восстание в Австрии
new-page: true
subtitle: В ответ на полицейские облавы, автрийцы подняли революцию...
view-if: year = 1934 and (month = 2 or month = 3) and austrian_parliament_seen
max-visits: 1
on-arrival: austrian_civil_war_seen = 1
face-image: img/schutzbund.png
tags: event

= Восстание в Австрии

# pre-emptive democratic victory if sdapo strength >= 6
# long civil war if sdapo_strength = 5 or 4 - opportunity for interference/forced peace
# instant defeat if sdapo_strength < 4

Кризис в Австрии продолжается. Авсрофашистское правительство Энгельберта Дольфуса продолжает править экстренными указами. Оно преследовало политических оппонентов, в основном социал-демократов и СДПА.

[? if sdapo_strength < 6: Теперь Австрия в состоянии гражданской войны. Когда полиция попыталась отобрать оружие у Республиканского Союза Защиты, Шутцбунд ответил силой, открыв огонь по полицейским. После этого по всей Австрии начались стычки социал-демократов с правительственными силами. ?]

[? if sdapo_strength >= 6: Попытки подавить социал-демократическую партию Австрии были встречены массовыми протестами, поддерживаемыми СДПГ[? if foreign_minister_party = "SPD":  и немецким правительством?]. [? if eu_austria: Также недавно образованный ЕС наложил санкции на правительство, поддержав повстанцев. ?] ?]

В итоге...

- @crushed
- @long_war
- @sdapo_victory

@crushed
title: Повстанческие силы были сокрушены.
on-arrival: austria_defeat = 1
view-if: sdapo_strength < 4

Восстание было подавлено спустя несколько дней после начала. Союз защиты и СДПА были слишком слабы и разрозненны, чтобы противостоять правительству.

@long_war
title: Началась гражданская война.
view-if: sdapo_strength = 5 or sdapo_strength = 4
on-arrival: austria_civil_war = 1

Ни одна их сторон не собирается отступать, а СДПА организовала собственное правительство в некоторых городах после победы над полицией. Австрия перешла в состояние настоящей гражданской войны.

- @support_sdapo_party
- @support_sdapo_govt
- @force_peace
- @long_war_2

@support_sdapo_party
title: Как партия с той же идеологией, СДПГ поддержит СДПА оружием и добровольцами.
subtitle: -2 resources
choose-if: resources >= 2
on-arrival: resources -= 2; sdapo_strength += 1
go-to: sdapo_victory if sdapo_strength >= 6; long_war_2 if sdapo_strength < 6

СДПГ собрала некоторые средства и использовала их для поддержки СДПА, отправив добровольцев и часть оружия со своих складов.

@support_sdapo_govt
title: МИД поддержит демократические силы.
view-if: spd_in_government and foreign_minister_party = "SPD"
on-arrival: sdapo_strength += 1
go-to: sdapo_victory if sdapo_strength >= 6; long_war_2 if sdapo_strength < 6

Мы дипломатически и финансово поддержали СДПА в их борьбе.

@force_peace
title: Мы можем принудить обе стороны к миру?
view-if: spd_in_government and foreign_minister_party = "SPD" and sdapo_strength = 5
on-arrival: austria_peace = 1; pro_republic += 3; coup_progress -= 1
achievement: arbeiter_von_wien

Австрийское правитеьство считает нас предвзятыми, так что они недовольны нашим участием в переговорах. Несмотря на это, процесс уже был начат. В Швейцарии, при участии фашистской Италии как стороны поддержки австрофашистов, был заключён мирный договор. Социалисты отказываются от революции и сдают всё оружие взамен на симметричные действия со стороны отрядов австрофашистов. Диктатура пала, и были восстановлены свободные выборы.

Учитывая позиции СДПА  вначале, исход можно считать победой. Теперь начинается долгий процесс восстановления...

@long_war_2
title: Мы ничего не можем поделать.
on-arrival: z_relation -= 6; dvp_relation -= 6; ddp_relation -= 3; catholics_spd -= 5; old_middle_spd -= 5; coalition_dissent += 1; economic_growth -= 0.5

Несмотря на наши усилия, война продолжается. Было потеряно ужасно много жизней. В Германию начинают стекаться беженцы, а торговля тем временем страдает. Многие называют наше вмешательство в Австрию ошибкой.


@sdapo_victory
title: Австрофашисты отступили!
on-arrival: pro_republic += 5; west_relation += 1; east_relation += 1; austria_victory = 1; reichswehr_loyalty += 0.1; coup_progress -= 2
view-if: sdapo_strength >= 6
achievement: arbeiter_von_wien

После полномасштабных протестов правительство отступило! Ему пришось уйти в отставку и провести свободные выборы. Теперь начинается процесс восстановления. Скорее всего, это приведёт к сближению между нашими странами.

[? if eu_austria: Это можно считать успехом новообразованного Европейского Союза и его дипломатических усилий. ?]
